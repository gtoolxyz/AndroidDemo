apply plugin: 'com.android.application'

// Load custom properties file signing.properties
Properties props = new Properties()
props.load(new FileInputStream(file(project.property('signing.properties'))))

repositories {
    mavenCentral()
}

android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    signingConfigs {
        release {
//            If you want to use another file like signing.properties...
            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']

//            If you want to use the default gradle.properties use directly...
//            storeFile file(project.STORE_FILE)
//            storePassword project.STORE_PASSWORD
//            keyAlias project.KEY_ALIAS
//            keyPassword project.KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            minifyEnabled true
            shrinkResources true
        }

        // Uncomment to see the results of using Proguard. NOTE: proguard is not needed while develop the app.
//        debug {
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
//            minifyEnabled true
//            shrinkResources true
//        }
    }

}

dependencies {
    compile 'com.google.inject:guice:3.0:no_aop'

    compile 'com.melnykov:floatingactionbutton:1.0.5'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'

    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup:otto:1.3.5'

    compile 'com.github.nicolasjafelle:qachee:1.1'
}


android.applicationVariants.all { variant ->

    variant.outputs.each { output ->
        def apk = output.outputFile

        def newName
        if (variant.buildType.debuggable ) {
            newName = 'Android Demo v' + project.VERSION_NAME + '_DEBUG' +'.apk';
        } else {
            newName = 'Android Demo v' + project.VERSION_NAME + '_RELEASE' + '.apk';
        }

        //noinspection GroovyAssignabilityCheck
        output.outputFile = new File(apk.parentFile, newName)
    }

}
